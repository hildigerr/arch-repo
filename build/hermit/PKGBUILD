# Maintainer: Hildigerr Vergaray <Maintainer@YmirSystems.com>
pkgname=termit-git
pkgver=3.1.r3
pkgrel=2
pkgdesc="Terminal emulator based on VTE library with Lua scripting"
arch=('i686' 'x86_64')
url="https://github.com/hildigerr/hermit"
license=('GPL')
depends=("lua" "vte3")
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
makedepends=("git" "cmake>=2.4")
source=("termit::git+${url}")
md5sums=('SKIP')

prepare() {
  cd "$srcdir/${pkgname%-git}"
  git checkout hermit
}

pkgver() {
  cd "$srcdir/${pkgname%-git}"
  git describe --tags --long --match="termit-*" | awk -F- '{printf "%s.r%s", $2, $3}'
}

build() {
  cd "$srcdir/${pkgname%-git}"
  cmake -DCMAKE_C_FLAGS="-I/usr/include/vte-2.91" -DCMAKE_INSTALL_PREFIX=/usr .
  make
}

package ()
{
  cd "$srcdir/${pkgname%-git}"
  make DESTDIR="$pkgdir" install
}
